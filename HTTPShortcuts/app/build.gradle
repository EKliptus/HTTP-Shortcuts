apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }

    defaultConfig {
        applicationId 'ch.rmy.android.http_shortcuts'
        minSdkVersion 16
        targetSdkVersion 29
        versionName rootProject.ext.versionName
        versionCode rootProject.ext.versionCode
        buildConfigField 'String', 'BUGSNAG_API_KEY', "\"${rootProject.ext.bugsnagAPIKey}\""
        buildConfigField 'String', 'REALM_ENCRYPTION_KEY', "\"${rootProject.ext.realmEncryptionKey}\""
        multiDexEnabled true

        testInstrumentationRunnerArguments = [package: "ch.rmy.android.http_shortcuts"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    dexOptions {
        jumboMode true
    }
}

project.configure(project.android, {
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
})

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
    google()
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    /* Support libraries */
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.2.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:viewmodel:1.1.1'

    /* UI */
    implementation 'com.satsuware.lib:usefulviews:2.3.1'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'

    /* Dialogs / Activities */
    implementation('com.afollestad.material-dialogs:core:3.1.1') {
        transitive = true
    }
    implementation 'com.afollestad.material-dialogs:input:3.1.1'
    implementation 'me.priyesh:chroma:1.0.2'
    implementation 'com.nononsenseapps:filepicker:4.2.1'

    /* Acknowledgments */
    implementation 'com.mikepenz:aboutlibraries:7.1.0'

    /* Image selection */
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    /* HTTP & Network */
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.burgstaller:okhttp-digest:1.12'
    implementation 'org.apache.httpcomponents:httpcore:4.4.11'

    /* Tasker integration */
    implementation 'com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3'

    /* Scripting */
    implementation 'com.github.LiquidPlayer:LiquidCore:0.6.2'

    /* Password hashing */
    implementation 'org.mindrot:jbcrypt:0.4'

    /* Crash Reporting */
    implementation 'com.bugsnag:bugsnag-android:4.21.1'

    /* cURL import & export */
    implementation project(path: ':curl_command')

    /* JSON serialization & deserialization */
    implementation 'com.google.code.gson:gson:2.8.6'

    /* RX */
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'

    // Testing
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.robolectric:shadows-multidex:$robolectricVersion"
    testImplementation 'androidx.test:monitor:1.1.1'

    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
    androidTestImplementation "junit:junit:$junitVersion"
}

apply plugin: 'realm-android'